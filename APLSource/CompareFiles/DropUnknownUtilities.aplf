 (exes_ names_)←DropUnknownUtilities(exes names);paths;exe;b;isWin;sep
⍝ If an EXE is neither a absolute path nor found on PATH it's dropped.
 sep←(1+isWin←'Win'≡A.GetOperatingSystem ⍬)⊃':;'
 paths←sep(≠⊆⊢)2 ⎕NQ #'GetEnvironment' 'PATH'
 paths/⍨←~':\WINDOWS'{b←':\WINDOWS'{⍺∘≡¨(≢⍺)↑¨1↓¨⍵}paths ⋄ b∧(⊃¨(1+≢⍺)↓¨⍵)∊¨⊂' \'}paths
 exes_←names_←''
 :For exe :In exes
     :If ':'∊exe                                    ⍝ Is it an ...
     :OrIf '/'=1⍴exe                                ⍝ ... absolute path ...
         :If F.IsFile exe                           ⍝ ... and does exist?
             exes_,←⊂exe                            ⍝ Then it's fine
             names_,←⊂(exes⍳⊂exe)⊃names
         :EndIf
     :Else
         :If ~isWin
             exe←2⊃⎕NPARTS exe
             exes←{2⊃⎕NPARTS ⍵}¨exes
         :EndIf
         b←F.IsFile paths,¨⊂'/',exe                 ⍝ To be found on PATH somewhere?
         :If ∨/b
             exes_,←⊂((b⍳1)⊃paths),'/',exe          ⍝ Then it's fine
             names_,←⊂(exes⍳⊂exe)⊃names
         :Else
             b←F.IsFile paths,¨⊂'/bin/',exe         ⍝ To be found on PATH/bin somewhere?
             :If ∨/b
                 exes_,←⊂((b⍳1)⊃paths),'/bin/',exe  ⍝ Then it's fine
                 names_,←⊂(exes⍳⊂exe)⊃names
             :EndIf
         :EndIf
     :EndIf
 :EndFor
⍝Done
